import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    alias(libs.plugins.spring.boot) apply false
    alias(libs.plugins.spring.dependency.management)
}

group = 'xyz.f3l1x'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    implementation libs.spring.boot.starter.data.jpa
    implementation libs.spring.content.fs.boot.starter
    implementation project(':core')
    implementation libs.model.mapper
    implementation libs.flyway
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testImplementation platform(testLibs.junit.bom)
    testImplementation testLibs.junit.jupiter
    runtimeOnly 'com.h2database:h2'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-parameters")
}